{
 "metadata": {
  "language": "Julia",
  "name": "",
  "signature": "sha256:bb5e57f15a12cd4897f44fba8a3cdfe787e5084e741f24bdaa9b6892720de4c6"
 },
 "nbformat": 3,
 "nbformat_minor": 0,
 "worksheets": [
  {
   "cells": [
    {
     "cell_type": "markdown",
     "metadata": {},
     "source": [
      "# Callbacks in Integer Programming\n",
      "\n",
      "As we discussed in the beginning, MIP solvers are complicated combinations of many techniques: cutting planes, heuristics, branching rules, etc.\n",
      "\n",
      "Some solvers allow you to customize aspects of the solve process in a deeper way than just setting options for these parameters. You can provide code to be run when certain events happen, and the solver **calls back** to these functions to ask what action(s) should be taken. Why might you want to do this?\n",
      "\n",
      "* The solver is struggling to find an integer solution. You know an efficient way to take a fractional solution and convert it to a good, if not optimal, integer solution. You can put this algorithm inside a **heuristic callback** that is called whenever a new fractional solution is found.\n",
      "* You have done an analysis of the structure of your MIP and have realized that you can find constraints that will cut off fractional solutions so that your LP relaxation is closer to integer points. You can write this as a **cut callback**.\n",
      "\n",
      "The particular example we will look at today is in some ways even more critical than these two types, because it enables whole types of problems to be solved that would be very difficult otherwise. In particular, consider a problem that has a very large number of constraints, **most of which will not be binding at the optimal solution**. This suggests that we probably don't need all those constraints to provided explicitly to the solver - instead, we can provide them implicitly with a **lazy constraint/cut callback**."
     ]
    },
    {
     "cell_type": "markdown",
     "metadata": {},
     "source": [
      "## Application: Robust Portfolio Optimization\n",
      "\n",
      "Portfolio optmization is the problem of constructing a portfolio of assets to maximize returns, but usually with some consideration towards the risk of the portfolio. If we maximize for return, we will usually also have the highest chance of losing money. On the other hand, there is often a (very) low risk option that has minimal returns (e.g. US government bonds). We seek to construct optimization models that are let us explore this spectrum of options.\n",
      "\n",
      "The \"stochastic programming\" approach would estimate a probability distribution from data for each asset we are considering purchasing, and then we can do things like\n",
      "\n",
      "- minimize $StdDev[Profit]$, subject to $Exp[Profit] \\geq P_{min}$\n",
      "- maximize $E[Profit]$, subject to $StdDev[Profit] \\leq S_{max}$\n",
      "\n",
      "**Robust optimization** is an alternative method that, instead of saying that the uncertain return of the assets coming from probability distributions, says the returns are drawn from a bounded set of outcomes: an **uncertainty set**.\n",
      "\n",
      "### Setting up the Problem\n",
      "\n",
      "We will consider the following robust portfolio problem.\n",
      "\n",
      "- Let $0 \\leq x_i \\leq 1$ be the share of our money we put into asset $i$.\n",
      "  - We need the additional constraint then that $\\mathbf{e}^\\prime \\mathbf{x} = 1$\n",
      "  - We'll also impose a restriction that we can use no more than a quarter of the assets available.\n",
      "  - Let $y_i \\in \\{0,1\\}$, $y_i = 1 \\iff x_i > 0$, and $\\mathbf{e}^\\prime \\mathbf{y} \\leq \\frac{N}{4}$\n",
      "\n",
      "- Let $p_i$ be the uncertain profit for asset $i$, with $\\mathbf{p}\\in U$, where...\n",
      "\n",
      "- $U$ is out uncertainty set. By varying its size and shape of the uncertainty set $U$ we can tradeoff between expected return and the worst-case return. We will assume we have (as data)\n",
      "  - $\\bar{p}_i$, the expected return of each asset\n",
      "  - $\\sigma_i$, the standard devition of return for each asset\n",
      "\n",
      "We will use the **ellipsoidal uncertainty set**\n",
      "\n",
      "$$ U^\\Gamma = \\left\\{ \\mathbf{p} \\mid p_i = \\bar{p}_i + \\sigma_i d_i, \\|\\mathbf{d}\\|\\leq \\Gamma \\right\\}$$\n",
      "\n",
      "So we can write out our problem now as\n",
      "\n",
      "$$\n",
      "\\max_{z, \\mathbf{x}\\geq \\mathbf{0}} z \\quad \\text{subject to}\\\\\n",
      "z \\leq \\mathbf{p}^\\prime \\mathbf{x} \\quad \\forall \\mathbf{p} \\in U \\\\\n",
      "\\mathbf{e}^\\prime \\mathbf{x} = 1 \\\\\n",
      "y_i \\geq x_i \\\\\n",
      "\\mathbf{e}^\\prime \\mathbf{y} \\leq \\frac{N}{4}\n",
      "$$\n",
      "\n",
      "The problem with the first constraint is that it is actually an **infinite** number of constraints - one for every possible value of $\\mathbf{p}$. We conjecture though that only a small number of them are needed to a get solution that \"mostly\" satisifes that constraint. We'll add them lazily using **lazy constraints** in JuMP with Gurobi. \n",
      "\n",
      "Whenever Gurobi finds a new integer-feasible solution $\\left( \\mathbf{x}^\\ast, \\mathbf{y}^\\ast, z^\\ast \\right)$, we will try to generate a new constraint. We do that by solving an **embedded** optimization problem:\n",
      "\n",
      "$$CUT(\\mathbf{x}^\\ast) = {\\arg \\min}_{\\mathbf{p}\\in U} \\mathbf{p}^\\prime \\mathbf{x}^\\ast$$\n",
      "\n",
      "We'll only add this new constraint if the it'd be violated by the current solution by more than a tolerance. Today we'll actually solve this embedded problem using Gurobi, but as an **exercise** you could solve it in closed-form - see how much of an improvement in solve times you get!"
     ]
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [
      "using JuMP, Gurobi\n",
      "\n",
      "# Generate data\n",
      "n = 20\n",
      "p\u0304 = [1.15 + i*0.05/150 for i in 1:n]\n",
      "\u03c3 = [0.05/450*\u221a(2*i*n*(n+1)) for i in 1:n]\n",
      "\n",
      "function solve_portfolio()\n",
      "    port = Model(solver=GurobiSolver())\n",
      "    \n",
      "    @defVar(port, z \u2264 maximum(p\u0304))\n",
      "    @setObjective(port, Max, z)\n",
      "    @defVar(port, 0 \u2264 x[1:n] \u2264 1)\n",
      "    @addConstraint(port, sum(x) == 1)\n",
      "    \n",
      "    @defVar(port, y[1:n], Bin)\n",
      "    for i in 1:n\n",
      "        @addConstraint(port, y[i] \u2265 x[i])\n",
      "    end\n",
      "    @addConstraint(port, sum(y) \u2264 div(n,4))\n",
      "    \n",
      "    # Link z to x\n",
      "    function portobj(cb)\n",
      "        # Get values of z and x\n",
      "        zval = getValue(z)\n",
      "        xval = getValue(x)[:]\n",
      "    \n",
      "        # Find most pessimistic value of p'x\n",
      "        # over all p in the uncertainty set\n",
      "        rob = Model(solver=GurobiSolver(OutputFlag=0))\n",
      "        @defVar(rob, p[i=1:n])\n",
      "        @defVar(rob, d[i=1:n])\n",
      "        @setObjective(rob, Min, dot(xval,p))\n",
      "        @addConstraint(rob, sum{d[i]^2,i=1:n} \u2264 10)\n",
      "        for i in 1:n\n",
      "            @addConstraint(rob, p[i] == p\u0304[i] + \u03c3[i]*d[i])\n",
      "        end\n",
      "        solve(rob)\n",
      "        worst_z = getObjectiveValue(rob)\n",
      "        @show (zval, worst_z)\n",
      "        worst_p = getValue(p)[:]\n",
      "        \n",
      "        # Is this worst_p going to change the objective\n",
      "        # because worst_z is worse than the current z?\n",
      "        if worst_z < zval - 1e-2\n",
      "            # Yep, we've made things worse!\n",
      "            # Gurobi should try to find a better portfolio now\n",
      "            @addLazyConstraint(cb, z \u2264 dot(worst_p,x))\n",
      "        end\n",
      "    end\n",
      "    setLazyCallback(port, portobj)\n",
      "    \n",
      "    solve(port)\n",
      "    \n",
      "    return getValue(x)[:]\n",
      "end\n",
      "\n",
      "solve_portfolio()"
     ],
     "language": "python",
     "metadata": {},
     "outputs": []
    },
    {
     "cell_type": "markdown",
     "metadata": {},
     "source": [
      "\n",
      "## Application: Travelling Salesman\n",
      "\n",
      "The most famous application of this is the **Travelling Salesman Problem**. TSP is the problem of finding a tour of shortest length that visits all the nodes in a graph. The decision variables in the MIP formulation correspond to whether we use an arc or not. If there $N$ nodes, we have $N^2$ variables. We will need $N$ constraints to make sure that each city is visited once. However, if you solve this you will find it is not sufficient:\n",
      "\n",
      "![subtours](http://i.imgur.com/rX9EYAr.png)\n",
      "\n",
      "To make sure these subtours don't occur, we need **subtour elimination constraints**. Unfortunately, there are $2^N$ possible subtour elimination constraints, which grows very very fast. The solution is to only add these constraints **lazily**: whenever the MIP solver finds an integer solution, we check for subtours. If we find them, we return a new constraint that will \"break\" the subtours. We then keep solving from there and repeat until no subtours our found. In practice we need far fewer than $2^N$ constraints, which is why it is possible to solve TSPs with 1000s of variables to optimality.\n",
      "\n"
     ]
    },
    {
     "cell_type": "code",
     "collapsed": false,
     "input": [],
     "language": "python",
     "metadata": {},
     "outputs": []
    }
   ],
   "metadata": {}
  }
 ]
}